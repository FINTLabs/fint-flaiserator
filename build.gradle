plugins {
    id 'org.springframework.boot' version '2.7.5'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'java'
    id 'groovy'
}

group = 'no.fintlabs'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    mavenLocal()
    repositories {
        maven {
            url "https://repo.fintlabs.no/releases"
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    annotationProcessor 'io.fabric8:crd-generator-apt:6.2.0'

    implementation 'no.fintlabs:flais-operator-starter:1.0.0-rc-11'
    //implementation 'no.fintlabs:flais-operator-starter:0-SNAPSHOT'

    implementation 'io.getunleash:unleash-client-java:9.2.0'

    compileOnly 'org.projectlombok:lombok'

    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    annotationProcessor 'org.projectlombok:lombok'

    testImplementation 'cglib:cglib-nodep:3.3.0'
    testImplementation 'org.spockframework:spock-spring:2.3-groovy-3.0'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('copyResourceDefinition', Copy) {
    from "$buildDir/classes/java/main/META-INF/fabric8/applications.fintlabs.no-v1.yml"
    into "$projectDir/kustomize/base"
}

tasks.named("build") { finalizedBy("copyResourceDefinition") }

tasks.named("clean") {
    doLast {
        project.delete("$projectDir/kustomize/base/applications.fintlabs.no-v1.yml")
    }
}
